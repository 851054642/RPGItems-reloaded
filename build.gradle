plugins {
    id 'java-library'
    id 'maven-publish'
}

ext.majorVersion = 3
ext.minorVersion = 6
ext.minecraftVersion = "1.13"

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenLocal()
    jcenter()
    mavenCentral()
    maven {
        name 'Spigot'
        url 'https://hub.spigotmc.org/nexus/content/groups/public/'
    }

    maven {
        name 'Sonatype'
        url 'https://oss.sonatype.org/content/groups/public'
    }

    maven {
        name 'sk89q'
        url "http://maven.sk89q.com/repo/"
    }
    maven {
        name 'NyaaCentral'
        url 'https://raw.githubusercontent.com/NyaaCat/NyaaCentral/maven-repo'
    }
}

dependencies {
    api 'org.spigotmc:spigot-api:1.13-R0.1-SNAPSHOT'
    implementation files('lib/worldguard-legacy-7.0.0-SNAPSHOT-dist.jar')
    implementation files('lib/worldedit-bukkit-7.0.0-SNAPSHOT-dist.jar')
    implementation('cat.nyaa:nyaacore:6.1-SNAPSHOT') {
        transitive = false
    }
    implementation 'com.google.code.findbugs:jsr305:1.3.9'
    compileOnly 'org.librazy:NyaaUtilsLangChecker:2.0-SNAPSHOT'
}

String nyaaCoreLangDir = System.getenv("NYAACORE_LANG_DIR")

compileJava {
    options.compilerArgs += ["-Xplugin:NyaaUtilsLangAnnotationProcessor", "-Xlint:deprecation", "-Xlint:unchecked"]
    if (nyaaCoreLangDir != null) {
        options.compilerArgs += ["-ALANG_DIR_ADDITIONAL_PATH=" + nyaaCoreLangDir]
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId "cat.nyaa"
            artifactId "rpgitems"
            version "$majorVersion.$minorVersion-SNAPSHOT"
            from components.java
        }
    }
    repositories {
        maven {
            url "$buildDir/repo"
        }
    }
}

javadoc {
    (options as StandardJavadocDocletOptions).with {
        links 'https://docs.oracle.com/javase/8/docs/api/'
        links 'https://hub.spigotmc.org/javadocs/spigot/'
        links 'https://google.github.io/guava/releases/21.0/api/docs/'
        links 'https://nyaacat.github.io/NyaaCentral/NyaaCore/6.1-mc1.13/javadoc/'
        links 'https://ci.md-5.net/job/BungeeCord/ws/chat/target/apidocs/'

        locale 'en_US'
        encoding 'UTF-8'
        docEncoding 'UTF-8'
        addBooleanOption('keywords', true)

        def currentJavaVersion = JavaVersion.current()
        if (currentJavaVersion > JavaVersion.VERSION_1_9) {
            options.addBooleanOption('html5', true)
        }

        windowTitle = "RPGItems Javadoc"
        String fullVersion = System.getenv("FULL_VERSION_STRING");
        if (fullVersion != null){
            docTitle = "<b>RPGItems</b> " + System.getenv("FULL_VERSION_STRING")
        } else {
            docTitle = "<b>RPGItems</b>"
        }
    }
}
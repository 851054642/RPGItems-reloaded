plugins {
    id 'java-library'
    id 'maven-publish'
}

String majorVersion = 3
String minorVersion = 8


sourceCompatibility = 1.8
targetCompatibility = 1.8

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

repositories {
    // mavenLocal()
    jcenter()
    mavenCentral()
    maven {
        name 'Spigot'
        url 'https://hub.spigotmc.org/nexus/content/groups/public/'
    }

    maven {
        name 'Sonatype'
        url 'https://oss.sonatype.org/content/groups/public'
    }

    maven {
        name 'sk89q'
        url "http://maven.sk89q.com/repo/"
    }

    maven {
        name 'NyaaCentral'
        url 'https://raw.githubusercontent.com/NyaaCat/NyaaCentral/maven-repo'
    }
}

jar {
    archiveClassifier.set('api')
}

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from sourceSets.main.java.srcDirs
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

dependencies {
    String minecraftVersion = "1.13.2"
    if (project.properties.minecraftVersion != null) {
        minecraftVersion = project.properties.minecraftVersion
    }
    compileOnly "org.spigotmc:spigot-api:$minecraftVersion-R0.1-SNAPSHOT"

    // compileOnly files('lib/spigot-1.13.2.jar')
    // compileOnly files('lib/patched_1.13.2.jar')
    compileOnly 'cat.nyaa:nyaacore:6.3-SNAPSHOT'

    compileOnly 'io.netty:netty-all:4.1.25.Final' // netty is shadowed inside spigot jar
    compileOnly 'org.ow2.asm:asm:7.0' // netty is shadowed inside spigot jar

    // shadow project(":powers:RPGItems-Power-1.13.2")
    // implementation files('lib/worldedit-bukkit-7.0.0-SNAPSHOT-dist.jar')
    // implementation files('lib/worldguard-legacy-7.0.0-SNAPSHOT-dist.jar')

    compileOnly('com.sk89q.worldguard:worldguard-core:7.0.0-SNAPSHOT')
    compileOnly('com.sk89q.worldguard:worldguard-legacy:7.0.0-SNAPSHOT') {
        exclude group: 'io.papermc', module: 'paperlib'
        exclude group: 'org.bstats', module: 'bstats-bukkit'
        exclude group: 'org.bukkit', module: 'bukkit'
    }

    compileOnly 'com.google.code.findbugs:jsr305:1.3.9'
    compileOnly 'cat.nyaa:LangUtils:2.1-SNAPSHOT'
    compileOnly 'org.librazy:NyaaUtilsLangChecker:2.0-SNAPSHOT'
}

tasks.withType(JavaCompile) {
    options.fork = true
    options.forkOptions.jvmArgs += ["-Duser.language=en"]
    options.compilerArgs += ["-Xlint:deprecation", "-Xlint:unchecked"]
}

javadoc {
    (options as StandardJavadocDocletOptions).with {
        links 'https://docs.oracle.com/javase/8/docs/api/'
        links 'https://hub.spigotmc.org/javadocs/spigot/'
        links 'https://google.github.io/guava/releases/21.0/api/docs/'
        links 'https://nyaacat.github.io/NyaaCentral/NyaaCore/6.3-mc1.13.2/javadoc/'
        links 'https://ci.md-5.net/job/BungeeCord/ws/chat/target/apidocs/'
        links 'https://netty.io/4.1/api/'

        locale 'en_US'
        encoding 'UTF-8'
        docEncoding 'UTF-8'
        addBooleanOption('keywords', true)

        def currentJavaVersion = JavaVersion.current()
        if (currentJavaVersion > JavaVersion.VERSION_1_9) {
            options.addBooleanOption('html5', true)
        }

        windowTitle = "RPGItems Javadoc"
    }
}